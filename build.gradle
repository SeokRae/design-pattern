buildscript {
    ext {
        springBootVersion = '2.5.6'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath "io.spring.gradle:dependency-management-plugin:1.0.10.RELEASE"
    }
}

allprojects {
    group = 'com.example'
    version = '0.0.1-SNAPSHOT'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    sourceCompatibility = JavaVersion.VERSION_11

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter-web'
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
    }

    test {
        useJUnitPlatform()
    }

    // 해당 task로 서브 모듈 추가시 기초적인 디렉토리를 자동으로 생성해줍니다.
    task initSourceFolders {
        sourceSets*.java.srcDirs*.each {
            if (!it.exists()) {
                it.mkdirs()
            }
        }

        sourceSets*.resources.srcDirs*.each {
            if (!it.exists()) {
                it.mkdirs()
            }
        }
    }
}

project(":creational") {
    dependencies {
        implementation 'org.modelmapper:modelmapper:2.1.1'
    }
}

project(":structural") {
    dependencies {
        // https://mvnrepository.com/artifact/org.springframework.boot/spring-boot-starter-mail
        implementation 'org.springframework.boot:spring-boot-starter-mail:1.2.0.RELEASE'
        // https://mvnrepository.com/artifact/org.springframework.boot/spring-boot-starter-jdbc
        implementation 'org.springframework.boot:spring-boot-starter-jdbc:2.5.6'
    }
}

project(":behavioural") {
}